Rem
        ScenLangLoader.bmx
	(c) 2015, 2017 Jeroen Petrus Broks.
	
	This Source Code Form is subject to the terms of the 
	Mozilla Public License, v. 2.0. If a copy of the MPL was not 
	distributed with this file, You can obtain one at 
	http://mozilla.org/MPL/2.0/.
        Version: 17.04.17
End Rem
Strict
Import brl.linkedlist
Import brl.map
Import jcr6.jcr6main
Import jcr6.zlibdriver
Import tricky_units.Listfile
Import tricky_units.advdatetime

Global InvalidTag

Type TLangBox
	Field PicDir$
	Field PicSpecific$ = "GENERAL"
	Field Heading$
	Field Text$
	Field NoTrim = False
	Field AltTxtFont$

	Method SplitText$[](Sep$="~n") Return Text.split(sep) End Method
	End Type
	
Type TLangList
	Field L:TList = New TList
	
	Method EAI:TLangBox(Idx) ' Entry at index
		Return TLangBox(L.valueatindex(Idx))
	End Method
	
	End Type
	
Type TLangTags
	Field TM:TMap = New TMap	
	
	Method LAT:TList(Tag$) ' List at tag
	Local r:Tlanglist = tlanglist(MapValueForKey(TM,Tag))
	Return r.L
	End Method

	Method LLAT:Tlanglist(Tag$) ' List at tag
	Local r:Tlanglist = tlanglist(MapValueForKey(TM,Tag))
	Return r
	End Method
	
	End Type

Private
Global ChkF(full$,dir$)[2]

Function YesTrueDir(full$,dir$) Return ExtractDir(Upper(full))=Upper(dir) End Function;   chkF[1]=YesTrueDir
Function NoTrueDir(Full$,dir$) Return Left(Upper(full),Len(dir))=Upper(dir) End Function; chkF[0]= NoTrueDir

Public
Function LangTagMap:TlangTags(M:TMap,Key$)
Return tlangtags(MapValueForKey(M,Key))
End Function

Function LoadFullLangFile:TMap(JCR:TJCRDir,Dir$="",TrueDir=False)
Local slash$
Local CF(full$,dir$)=ChkF[TrueDir]
Local ret:TMap = CreateMap()
If dir And Right(dir,1)<>"/" slash="/"
For Local F$ = EachIn MapKeys(JCR.entries)
	If CF(F,Dir) 
		MapInsert ret,Right(F,Len(f)-Len(dir+slash)),LoadLangList(JCR,F)
		DebugLog "Added entry: "+Right(F,Len(f)-Len(dir+slash))
		Else
		DebugLog "Entry "+F+" has been refused!"
		EndIf
	Next
?debug
Print "DEBUG TESTING LANG FILE:> Have all the entries been added?"
For Local K$=EachIn MapKeys(ret)
	Print "DEBUG TESTING LANG FILE:> Entry "+K+" has been added!"
	Next
?		
Return ret	
End Function

Function LoadLangList:TLangTags(JCR:TJCRDir,File$)
Local Ret:Tlangtags = New TLangtags
Local section$="[rem]"
Local TL$,UPL$,T$
Local CBox:TLangBox
Local Centry:TLanglist
For Local L$=EachIn Listfile(JCR_B(JCR,FIle))
	TL = Trim(L)
	If Left(TL,1)="[" And Right(TL,1)="]"
		section = Lower(TL)
	ElseIf TL
		Select section
			Case "[rem]"
				' Do nothing! It's a remark!
			Case "[tags]"
				MapInsert ret.tm,tl,New tlanglist
				DebugLog "Added tag: "+TL
			Case "[scenario]"
				UPL = Right(TL,Len(TL)-1) ' UnPrefixedLine
				If (Chr(TL[0])<>"@" And Chr(TL[0])<>"-") And (Not CBox)
					Print "Cannot assign data when no textbox is created"
				Else	
					Select Chr(TL[0])
						Case "@"
							If Not Trim(UPL)
								Print "WARNING! Empty Tag found!"
								invalidtag=True
							Else
								invalidtag=False
								CBox = New TLangbox
								CEntry = tlanglist(MapValueForKey(ret.tm,UPL))
								If Not centry 
									Print "There is no scenario tag named: "+UPL
									Return
									EndIf
								ListAddLast CEntry.L,CBox
								EndIf	
						Case "!"
							If Not invalidtag CBox.Heading = UPL
						Case "*"
							If Not invalidtag CBox.PicDir = UPL
						Case ":"
							If Not invalidtag CBox.PicSpecific = UPL
						Case "#"
							If Not invalidtag
								If CBox.Text CBox.Text:+"~n"
								CBox.Text :+ UPL
								EndIf
						Case "%"
							If Not invalidtag CBox.AltTxtfont = UPL	
						Case "-"
							' Used for comments if needed! ;)
						Default
							Print "Unknown command character "+Chr(TL[0])+TL[0]+"  >>> "+TL						
						End Select
					EndIf	
			Default
				Print "Unknown section type: "+section
				Return
			End Select				
		EndIf
	Next	
Return ret	
End Function	

Function SaveFullLangFile(url$,m:TMap,license$="",Copyrightholder$="",pprefixpath$="")
Local prefixpath$ = pprefixpath
If prefixpath And Right(prefixpath,1)<>"/" prefixpath:+"/"
Local JCR:TJCRCreate = JCR_Create(url)
Local CTM:TLangTags
Local BTC:TJCRCreateStream,BT:TStream,KK$
Local CTB:Tlangbox
For Local K$=EachIn MapKeys(M)
	btC = JCR.createEntry(K,"zlib")
	bt = btc.stream
	CTM = TLangtags(MapValueForKey(m,K))
	If Not CTM JCR.close; Print "Illegal tagmap "+K; Return	
	WriteLine bt,"[rem]~nThis file was generated by the ScenLang builder"
	If copyrightholder WriteLine bt,"(c) Copyright "+Year()+" "+copyrightholder
	If license WriteLine bt,license
	WriteLine bt,"~n~n~n[tags]"
	For KK$=EachIn MapKeys(CTM.TM) WriteLine bt,KK Next
	WriteLine bt,"~n~n~n[scenario]"
	For KK=EachIn MapKeys(CTM.TM)
		WriteLine bt,"~n-- Scenario for tag: "+KK+" --~n"
		For ctb=EachIn(CTM.LAT(KK))
			WriteLine bt,"~n"
			WriteLine bt,"@"+KK
			WriteLine bt,"!"+ctb.Heading
			WriteLine bt,"*"+ctb.PicDir
			WriteLine bt,":"+ctb.PicSpecific
			If ctb.alttxtfont WriteLine bt,"%"+ctb.alttxtfont
			If Not ctb.notrim ctb.text = Trim(ctb.text)
			For Local Line$=EachIn ctb.SplitText()
				WriteLine bt,"#"+line
				Next
			Next	
		Next
	btc.close	
	Next
jcr.close "zlib"	
Return True
End Function
